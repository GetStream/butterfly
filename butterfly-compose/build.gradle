import io.getstream.butterfly.Configuration
import io.getstream.butterfly.Dependencies
import io.getstream.butterfly.Versions

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'binary-compatibility-validator'
}

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    if (snapshot) {
        PUBLISH_VERSION = Configuration.snapshotVersionName
    } else {
        PUBLISH_VERSION = Configuration.versionName
    }
    PUBLISH_ARTIFACT_ID = 'butterfly-compose'
}

apply from: "${rootDir}/scripts/publish-module.gradle"

android {
    compileSdkVersion Configuration.compileSdk
    defaultConfig {
        minSdkVersion Configuration.minSdk
        targetSdkVersion Configuration.targetSdk
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.COMPOSE
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xexplicit-api=strict"]
}

dependencies {
    api project(":butterfly")

    implementation Dependencies.androidxAppcompat
    implementation Dependencies.composeUI
    implementation Dependencies.composeRuntime
}

apply from: "$rootDir/spotless.gradle"